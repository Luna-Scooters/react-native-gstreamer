apply plugin: 'com.android.library'

android {
    compileSdkVersion 36

    defaultConfig {
        minSdkVersion 25
        targetSdkVersion 36
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            ndkBuild {
                def gstRoot

                if (project.hasProperty('gstAndroidRoot'))
                    gstRoot = project.gstAndroidRoot

                if (gstRoot == null)
                    throw new GradleException('GSTREAMER_ROOT_ANDROID must be set, or "gstAndroidRoot" must be defined in your gradle.properties in the top level directory of the unpacked universal GStreamer Android binaries')

                // Absolute paths so ndk-build works when NDK_PROJECT_PATH=null (AGP/NDK 27+, e.g. GitHub Actions)
                def appMk = file('src/main/jni/Application.mk').absolutePath
                def javaSrcDir = file('src/main/java').absolutePath
                def assetsDir = file('src/main/assets').absolutePath
                arguments "NDK_APPLICATION_MK=$appMk", "GSTREAMER_JAVA_SRC_DIR=$javaSrcDir", "GSTREAMER_ROOT_ANDROID=$gstRoot", "GSTREAMER_ASSETS_DIR=$assetsDir"

                targets "rctgstplayer"

                abiFilters 'x86', 'armeabi-v7a', 'arm64-v8a', 'x86_64'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    sourceSets.main {
        jni.srcDirs = ["../../common"] // This prevents the auto generation of Android.mk
        //jniLibs.srcDir 'src/main/libs' // This is not necessary unless you have precompiled libraries in your project.
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.facebook.react:react-native:+'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
